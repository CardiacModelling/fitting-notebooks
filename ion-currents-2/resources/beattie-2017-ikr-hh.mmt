[[model]]
name: Beattie-2017-IKr-HH
author: Michael Clerx
# Initial values
ikr.act = 0
ikr.rec = 1

#
# Simulation engine variables
#
[engine]
time = 0 [ms]
    bind time

#
# Membrane potential
#
[membrane]
V = 0 [mV]
    bind pace
    label membrane_potential

[nernst]
EK = -85 [mV]

#
# Hodgkin-Huxley current model
#
[ikr]
use membrane.V
IKr = g * act * rec * (V - nernst.EK)
    in [nA]
dot(act) = (inf - act) / tau
    inf = k1 * tau
    tau = 1 / (k1 + k2)
    k1 = p1 * exp(p2 * V)
    k2 = p3 * exp(-p4 * V)
dot(rec) = (inf - rec) / tau
    inf = k4 * tau
    tau = 1 / (k3 + k4)
    k3 = p5 * exp(p6 * V)
    k4 = p7 * exp(-p8 * V)
p1 = 2.26e-4 [1/ms]
p2 = 0.0699 [1/mV]
p3 = 3.45e-5 [1/ms]
p4 = 0.05462 [1/mV]
p5 = 0.0873 [1/ms]
p6 = 8.91e-3 [1/mV]
p7 = 5.15e-3 [1/ms]
p8 = 0.03158 [1/mV]
p9 = 0.1524 [uS]
g = p9

